plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'net.researchgate.release' version '2.8.1'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'maven-publish'
    id 'signing'
}

repositories {
    jcenter()
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

shadowJar {
    archiveClassifier = 'shadow'
    relocate 'org.objectweb.asm', 'org.objectweb.asm.shaded'
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}

dependencies {
    api 'com.github.jnr:jnr-ffi:2.2.7'
    api 'com.github.jnr:jnr-posix:3.1.10'
    api 'com.github.jnr:jnr-constants:0.10.2'

    testImplementation 'junit:junit:4.13.2'
}

release {
    newVersionCommitMessage = '[f1r3flyfs release] - '
    preTagCommitMessage = '[f1r3flyfs release] - pre tag commit: '
    buildTasks = ['clean', 'assemble', 'publishToSonatype', 'closeAndReleaseSonatypeStagingRepository']
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'f1r3flyfs'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'f1r3flyfs'
                description = 'f1r3flyfs is a FUSE implementation in java using Java Native Runtime (JNR) and Rholang.'
                url = 'https://github.com/SerCeMan/jnr-fuse'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'SerCe'
                        name = 'Sergey Tselovalnikov'
                        email = 'sergeicelov@gmail.com'
                    }
                }

                scm {
                    url = 'https://github.com/SerCeMan/jnr-fuse'
                    connection = 'scm:git:git@github.com:SerCeMan/jnr-fuse.git'
                    developerConnection = 'scm:git:git@github.com:SerCeMan/jnr-fuse.git'
                }
            }
        }
    }

    repositories {
        maven {
            // A test repository in the build repository
            // allowing easy verification of published artifacts
            name = 'projectLocal'
            url = file("${buildDir}/repo")
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username.set(providers.environmentVariable("SONATYPE_LOGIN").forUseAtConfigurationTime())
            password.set(providers.environmentVariable("SONATYPE_PASSWORD").forUseAtConfigurationTime())
        }
    }
}

signing {
    sign publishing.publications.mavenJava
    setRequired {
        gradle.taskGraph.allTasks.any {
            it.name == 'publishToSonatype'
        }
    }
}
